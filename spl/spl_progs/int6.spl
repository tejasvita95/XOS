alias physicalSP S0;
physicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);
alias sysCallNo S1;
sysCallNo = [physicalSP - 1];
alias filename S2;
filename = [physicalSP - 3];
alias currentPID S3;
currentPID = (PTBR - 1024) / 8;
alias currentPCB S4;
currentPCB = READY_LIST + (currentPID * 32);

if(sysCallNo == 9) then
	alias fat S5;
	fat = 2560;
	while(fat < 3071) do
		if([fat + 0] == filename) then
			break;
		endif;
		fat = fat + 8;
	endwhile;
	if(fat >= 3071) then
		[physicalSP - 2] = -1;
		ireturn;
	endif;
	
	alias basicBlock S6;
	basicBlock = [fat + 2];
	load(1, basicBlock);

	alias countCodeBlock S7;
	countCodeBlock = 0;
	alias i S8;
	i = 512;
	while(i < 1024) do
		if([i] > 23 && [i] < 448) then
			countCodeBlock = countCodeBlock + 1;
		endif;
		i = i + 1;
	endwhile;

	alias countCodePages S9;
	alias j S10;
	countCodePages = 0;
	i = 1;
	while(i < 6) do
		if([PTBR + i] == "01" || [PTBR + i] == "11") then
			countCodePages = countCodePages + 1;
		endif;
		i = i + 2;
	endwhile;

	if(countCodePages > countCodeBlock) then
		i = countCodeBlock * 2;
		while(i < 6) do
			[1280 + [PTBR + i]] = 0;
			[PTBR + i] = -1;
			[PTBR + i + 1] = "00";
			i = i + 2;
		endwhile;
	endif;
	
	if(countCodePages < countCodeBlock) then
		i = countCodePages;
		while(i < countCodeBlock) do
			j = 1280;
			while(j < 1344) do
				if([j] == 0) then
					[PTBR + (i * 2)] = j - 1280;
					[PTBR + (i * 2) + 1] = "01";
					[j] = 1;
					break;
				endif;
				j = j + 1;
			endwhile;
			i = i + 1;
		endwhile;
	endif;

	i = 512;
	j = 0;
	while(i < 1024) do
		if([i] > 23 && [i] < 448) then
			load([PTBR + j], [i]);
			j = j + 2;
		endif;
		i = i + 1;
	endwhile;

	i = currentPCB + 15;
	while(i < currentPCB + 31) do
		if([i + 0] != -1) then
			[FILE_TABLE + ([i] * 2) + 1] = [FILE_TABLE + ([i] * 2) + 1] - 1;
			if([FILE_TABLE + ([i] * 2) + 1] == 0) then
				[FILE_TABLE + ([i] * 2)] = -1;
			endif;
			[i + 0] = -1;
			[i + 1] = -1;
		endif;
		i = i + 2;
	endwhile;

	SP = 3 * 512;
	[[PTBR + 6] * 512] = 0;
	ireturn;
endif;
if(sysCallNo == 11) then
	[physicalSP - 2] = currentPID;
	ireturn;
endif;
if(sysCallNo == 12) then
	if([currentPCB + 31] >= 0 && [currentPCB + 31] <= 31) then
		[physicalSP - 2] = [currentPCB + 31];
	else
		[physicalSP - 2] = -1;
	endif;
	ireturn;
endif;
